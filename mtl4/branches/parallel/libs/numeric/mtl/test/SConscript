import glob, re

Import('debug_env mpi_debug_env check')

# print "SConscript: debug_env[CXX] = ", debug_env['CXX']

sources = glob.glob("*.cpp")

# Works only with MTL_DEEP_COPY_CONSTRUCTOR
#	     block_sparse_test.cpp
#	     variable_size_block_sparse_test.cpp
sources.remove('block_sparse_test.cpp')
sources.remove('variable_size_block_sparse_test.cpp')

programs, mpi_programs = [], []
for p in sources:
    if 'mpi_' == p[0:4]:
        mpi_programs.append(mpi_debug_env.Program(p))
    else:
        programs.append(debug_env.Program(p))

if int(check):
    for p in programs:
        debug_env.AddPostAction(p, p[0].abspath)
    for p in mpi_programs:
        tmatch = re.search('mpi_(\d*).*', str(p))
        mpi_debug_env.AddPostAction(p, 'mpirun -np ' + tmatch.group(1) + ' ' + p[0].abspath)

Default(programs + mpi_programs)

# To run only mpi programs: scons -D check=1 `ls mpi*.cpp | sed s/\.cpp//g`  